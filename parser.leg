%{
#include <stdio.h>
#include "ast.h"
#include "symbol.h"
#include "exception.h"

static int eat_eol = 0;

#define YYSTYPE ast_t *

/* 
   Increase the parse buffers
*/

#define YY_BUFFER_START_SIZE 16384 

#define YY_STACK_SIZE YY_BUFFER_START_SIZE

#define YY_INPUT(buf, result, max_size, core)          \
{                                                      \
  int yyc = fgetc(stdin);                              \
  if (yyc == EOF) { eat_eol = 0; longjmp(exc, 2); }    \
  if (eat_eol) while (yyc == '\n') yyc = fgetc(stdin); \
  if (yyc == '(') eat_eol++;                           \
  if (yyc == ')') eat_eol--;                           \
  result = (EOF == yyc) ? 0 : (*(buf)= yyc, 1);        \
}
%}

start         = Spacing EOL { root = NULL; }
                 | Spacing r:Statement EOL { root = r; }
                 | ( !EOL .)* EOL { root = NULL; eat_eol = 0; printf("Syntax error\n"); }
Statement     = Spacing IfElseStmt
                 | Spacing Assignment
                 | Spacing Expression
Assignment    = i:Identifier Equals e:Expression { $$ = ast2(T_ASSIGN, i, e); }
Block         = s:Statement EOL b:Block { s->next = b; $$ = s; }
               | Statement 
               | Expression
IfElseStmt    = If e:Expression ( EOL | Then ) b:Block Else c:Block End 
              { $$ = ast3(T_IF_ELSE_STMT, e, ast1(T_THEN, b), ast1(T_ELSE, c)); }
Expression    = r:Infix40 ( ( EQ s:Infix40 { r = ast_binop(sym_lookup("=="), r, s); } )
               | ( NE s:Infix40 { r = ast_binop(sym_lookup("!="), r, s); } ) )* { $$ = r; } 
Infix40       = r:Infix20 ( ( LE s:Infix20 { r = ast_binop(sym_lookup("<="), r, s); } )
               | ( GE s:Infix20 { r = ast_binop(sym_lookup(">="), r, s); } ) 
               | ( LT s:Infix20 { r = ast_binop(sym_lookup("<"), r, s); } )
               | ( GT s:Infix20 { r = ast_binop(sym_lookup(">"), r, s); } ) )* { $$ = r; } 
Infix20       = r:Infix10 ( ( Plus s:Infix10 { r = ast_binop(sym_lookup("+"), r, s); } )
               | ( Minus s:Infix10 { r = ast_binop(sym_lookup("-"), r, s); } ) )* { $$ = r; }
Infix10       = r:Primary ( ( Times s:Primary { r = ast_binop(sym_lookup("*"), r, s); } )
               | ( Div s:Primary { r = ast_binop(sym_lookup("/"), r, s); } ) 
               | ( Mod s:Primary { r = ast_binop(sym_lookup("%"), r, s); } ) )* { $$ = r; }
Primary       = Integer | Identifier
                 | ( LParen Expression RParen )
                
Reserved      = If | Then | Else | End
Identifier    = !Reserved < IdentStart IdentCont* > Spacing
              {
                 sym_t * sym = sym_lookup(yytext);
                 $$ = ast_symbol(T_IDENT, sym);
              }
IdentStart    = [a-zA-Z_]
IdentCont     = IdentStart | [0-9]
Integer       = < ( [1-9] [0-9]* | '0' ) > Spacing
              {
                 sym_t * sym = sym_lookup(yytext);
                 $$ = ast_symbol(T_INT, sym);
              }
Spacing       = ( Space | Comment )*
Sp2           = ( Space | EOL | Comment )*
Space         = ' ' | '\t'
Comment       = '/*' ( !'*/' . )* '*/'
If            = Sp2 'if' Sp2
Then          = Sp2 'then' Sp2
Else          = Sp2 'else' Sp2
End           = Sp2 'end' Spacing
Equals        = '=' Sp2
LParen        = '(' Spacing
RParen        = ')' Spacing 
Plus          = '+' Sp2
Minus         = '-' Sp2
Times         = '*' Sp2
Div           = '/' Sp2
Mod           = '%' Sp2
EQ            = '==' Sp2
NE            = '!=' Sp2
LE            = '<=' Sp2
GE            = '>=' Sp2
LT            = '<' Sp2
GT            = '>' Sp2
EOL           = '\r\n' | '\n' | '\r'